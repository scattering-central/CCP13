C     LAST UPDATE 16/03/89
C+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
C
      SUBROUTINE POLNOM (XAXIS)
      IMPLICIT NONE
C
C Purpose: Calculate polynomial
C
      LOGICAL XAXIS
C
      INCLUDE 'COMMON.FOR'
C
C CALLS   4: GETHDR , OPNFIL , APLOT  , DAWRT
C CALLED BY: OTOKO
C
C-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-
C LOCAL VARIABLES:
C
      REAL    VALUE(10),XMIN,XMAX
      INTEGER ICLO,JOP,IRC,IFRAME,IHFMAX,IFRMAX,IMEM
      INTEGER KREC,ISPEC,LSPEC,MEM,IFFR,ILFR,INCR,IFINC
      INTEGER NCHANX,NCHAN,NVAL,ICH1,ICH2,NPTS,MDEG,NFRAME,I,J,K
      CHARACTER*80 HEAD1,HEAD2
      CHARACTER*13 HFNAM,OFNAM
C
C ICLO   : CLOSE FILE INDICATOR
C JOP    : OPEN FILE INDICATOR
C IRC    : RETURN CODE
C IFRAME : TOTAL NOS. OF FRAMES
C IFRMAX : NOS. OF FRAMES IN SEQUENCE
C IHFMAX : NOS. OF HEADER FILE IN SEQUENCE
C KREC   : OUTPUT FILE RECORD
C ISPEC  : FRAME NOS. PART OF FILENAME
C IFFR   : FIRST FRAME IN SEQUENCE
C ILFR   : LAST FRAME IN SEQUENCE
C INCR   : HEADER FILE INCREMENT
C IFINC  : FRAME INCREMENT
C HFNAM  : HEADER FILENAME
C OFNAM  : Output header filename
C HEAD1  : Output header record 1
C HEAD2  : Output header record 2
C IMEM   : OUTPUT MEMORY DATASET
C
      DATA  IMEM/1/
C
C-----------------------------------------------------------------------
      IF (XAXIS) THEN
         WRITE (IPRINT,*) 'X-Axis'
         CALL GETXAX (NCHANX,IRC)
         IF (IRC.NE.0) GOTO 999
      ENDIF
10    ICLO=0
      JOP=0
      KREC=0
C
      CALL GETHDR (ITERM,IPRINT,IUNIT,HFNAM,ISPEC,LSPEC,INCR,MEM,IFFR,
     1             ILFR,IFINC,IHFMAX,IFRMAX,NCHAN,IRC)
      IF (IRC.NE.0) GOTO 999
      IFRAME=IHFMAX+IFRMAX-1
C
      CALL GETCHN (ITERM,IPRINT,NCHAN,ICH1,ICH2,IRC)
      IF (IRC.NE.0) GOTO 10
20    WRITE (IPRINT,1000)
      CALL GETVAL (ITERM,VALUE,NVAL,IRC)
      IF (IRC.EQ.1) GOTO 10
      IF (IRC.EQ.2) GOTO 20
      IF (NVAL.EQ.0) THEN
         MDEG=1
      ELSE
         MDEG=INT(VALUE(1))
      ENDIF
      IF (.NOT.XAXIS) THEN
         DO 15 I=1,NCHAN
            XAX(I)=REAL(I)
15       CONTINUE
      ENDIF
      NPTS=ICH2-ICH1+1
C
      DO 60 I=1,IHFMAX
C
         CALL OPNFIL (JUNIT,HFNAM,ISPEC,MEM,IFFR,ILFR,NCHAN,NFRAME,IRC)
         IF (IRC.EQ.0) THEN
C
            ISPEC=ISPEC+INCR
            DO 50 J=1,IFRMAX
               READ (JUNIT,REC=IFFR) (SP1(K),K=1,NCHAN)
               IFFR=IFFR+IFINC
               KREC=KREC+1
C
               CALL FILL (SP2,NCHAN,1.0)
               CALL VC01A (XAX(ICH1),SP1(ICH1),SP2,SP4,NPTS,SP5,SP6,
     1                     SP7,SP8,SP9,SP10,MDEG,SPW)
               CALL POLCAL (XAX(ICH1),SP1(ICH1),SP5,SP6,SP7,MDEG,1,NPTS)
               CALL FILL (SP2,NCHAN,0.0)
               DO 40 K=ICH1,ICH2
                  SP2(K)=SP1(K)
40             CONTINUE
               IF (MDEG.EQ.1) THEN
                  XMIN=(SP1(ICH2)-SP1(ICH1))/(XAX(ICH2)-XAX(ICH1))
                  XMAX=SP1(ICH2)-XAX(ICH2)*XMIN
                  WRITE (IPRINT,1010) (IFFR-IFINC),XMIN,XMAX
               ENDIF
               IF (IFRAME.EQ.1) CALL APLOT (ITERM,IPRINT,SP2,NCHAN)
               IF (KREC.GE.IFRAME) ICLO=1
               IF (KREC.EQ.1) THEN
                  CALL OUTFIL (ITERM,IPRINT,OFNAM,HEAD1,HEAD2,IRC)
                  IF (IRC.NE.0) GOTO 70
               ENDIF
               CALL DAWRT (KUNIT,OFNAM,IMEM,NCHAN,IFRAME,HEAD1,HEAD2,
     1                     SP2,KREC,JOP,ICLO,IRC)
               IF (IRC.NE.0) GOTO 70
50          CONTINUE
            CLOSE (UNIT=JUNIT)
         ENDIF
60    CONTINUE
70    CLOSE (UNIT=JUNIT)
      GOTO 10
999   RETURN
C
1000  FORMAT (' Enter degree of polynomial [1]: ',$)
1010  FORMAT (' Frame: ',I3,' Slope: ',G13.5,' Intercept: ',G13.5)
      END

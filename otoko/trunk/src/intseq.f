C     LAST UPDATE 16/03/89
C+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
C
      SUBROUTINE INTSEQ (XAXIS)
      IMPLICIT NONE
C
C PURPOSE: INTEGRATE SEQUENTIALLY USING SAME SEQUENCE INTERVAL, OR
C          INTEGRATE ACCORDING TO A GIVEN SEQUENCE CONTAINED IN A FILE
C          GENERATED BY THE .GEN INSTRUCTION. A RANGE CONSISTS OF A
C          SERIES OF FIRST AND LAST VALUES.
C
      INCLUDE 'COMMON.FOR'
      LOGICAL XAXIS
C
C Calls   7: DAWRT , ERRMSG , FILL , GETHDR , GETVAL , OPNFIL , OUTFIL 
C Called by: OTOKO
C
C-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-
C Local variables:
C
      REAL    VALUE(20)
      INTEGER ICLO,JOP,IRC,IHFMAX,IFRMAX,IMEM,NCHAN,NFRAME
      INTEGER KREC,ISPEC,LSPEC,MEM,IFFR,ILFR,INCR,IFINC,NCHANX,NVAL
      INTEGER ISECT,JCH1,JCH2,MCH1,MCH2,ISEQ,ISAVE1,ISAVE2,I,J,K,L,M
      CHARACTER*80 HEAD1,HEAD2
      CHARACTER*13 HFNAM,OFNAM
C
C ICLO   : Close file indicator
C JOP    : Open file indicator
C IRC    : Return code
C IFRAME : Total nos. of frames
C IFRMAX : Nos. of frames in sequence
C IHFMAX : Nos. of header file in sequence
C KREC   : Output file record
C ISPEC  : First header file of sequence
C LSPEC  : Last header file in sequence
C MEM    : Positional or calibration data indicator
C IFFR   : First frame in sequence
C ILFR   : Last frame in sequence
C INCR   : Header file increment
C IFINC  : Frame increment
C HFNAM  : Header filename
C OFNAM  : Output header filename
C IMEM   : Output memory dataset
C NCHAN  : Nos. of data points in spectrum
C NFRAME : Nos. of time frames
C VALUE  : 
C
      DATA  IMEM/1/
C
C-----------------------------------------------------------------------
      IF (XAXIS) THEN
         WRITE (IPRINT,*) 'Integration Sequence'
         CALL GETXAX (NCHANX,IRC)
         IF (IRC.NE.0) GOTO 999
         ISECT=NCHANX/2
      ENDIF
10    KREC=0
      JOP=0
      ICLO=0
C
      CALL GETHDR (ITERM,IPRINT,IUNIT,HFNAM,ISPEC,LSPEC,INCR,MEM,IFFR,
     &             ILFR,IFINC,IHFMAX,IFRMAX,NCHAN,IRC)
      IF (IRC.NE.0) GOTO 999
C
      IF (.NOT.XAXIS) THEN
20       WRITE (IPRINT,1000) NCHAN
         CALL GETVAL (ITERM,VALUE,NVAL,IRC)
         IF (IRC.EQ.1) GOTO 60
         IF (IRC.EQ.2) GOTO 20
         IF (NVAL.LE.0) GOTO 20
         IF (NVAL.GE.1) JCH1=INT(VALUE(1))
         IF (NVAL.GE.2) JCH2=INT(VALUE(2))
         IF (NVAL.GE.3) ISEQ=INT(VALUE(3))      
         ISECT=(JCH2-JCH1+1)/ISEQ
      ENDIF
C
      DO 50 I=1,IHFMAX
C
         DO 45 J=1,ISECT
            CALL OPNFIL (JUNIT,HFNAM,ISPEC,MEM,IFFR,ILFR,NCHAN,
     &                   NFRAME,IRC)
            IF (IRC.NE.0) GOTO 60
C
            CALL FILL (SP2,IFRMAX,0.0)
            ISAVE1=ISPEC
            ISAVE2=IFFR
            ISPEC=ISPEC+INCR
            DO 40 K=1,IFRMAX
               READ (JUNIT,REC=IFFR) (SP1(L),L=1,NCHAN)
               IFFR=IFFR+IFINC
               KREC=KREC+1
C
               IF (XAXIS) THEN
                  MCH1=INT(XAX(2*J-1))
                  MCH2=INT(XAX(2*J))
                  DO 15 M=MCH1,MCH2
                     SP2(KREC)=SP2(KREC)+SP1(M)
15                CONTINUE
               ELSE
                  MCH1=JCH1+(J-1)*ISEQ
                  MCH2=MCH1+ISEQ-1
                  DO 16 M=MCH1,MCH2
                     SP2(KREC)=SP2(KREC)+SP1(M)
16                CONTINUE
                  SP2(KREC)=SP2(KREC)/REAL(ISEQ)
               ENDIF
40          CONTINUE
            IF (J.EQ.1) THEN
               CALL OUTFIL (ITERM,IPRINT,OFNAM,HEAD1,HEAD2,IRC)
               IF (IRC.NE.0) GOTO 60
            ENDIF
            IF (J.GE.ISECT) ICLO=1
            CALL DAWRT (KUNIT,OFNAM,IMEM,IFRMAX,ISECT,HEAD1,
     &                  HEAD2,SP2,J,JOP,ICLO,IRC)
            IF (IRC.NE.0) GOTO 60
            CLOSE (UNIT=JUNIT)
            ISPEC=ISAVE1
            IFFR=ISAVE2
            KREC=0
45       CONTINUE
50    CONTINUE
60    CLOSE (UNIT=JUNIT)
      GOTO 10
999   RETURN
C
1000  FORMAT (' Enter first, last & channel increment in sequence [',
     &        '1,',I4,',1]: ',$)
      END

C     LAST UPDATE 16/03/889
C+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
C
      SUBROUTINE FPK (XAXIS)
      IMPLICIT  NONE
C
C Purpose: Averages a range of frames according to a given packing 
C          sequence contained in a file generated by the .GEN
C          instruction. A range consists of a first and last frame.
C          Negative frame numbers are not packed.
C          Else it uses the frame increment if no .gen file supplied.
C
      LOGICAL XAXIS
C
      INCLUDE 'COMMON.FOR'
C
C Calls   4: GETHDR , OPNFIL , DAWRT , OUTFIL
C Called by: OTOKO
C
C-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-
C Local variables:
C
      INTEGER ICLO,JOP,IRC,IFRAME,IHFMAX,IFRMAX,IMEM,NCHAN,NFRAME
      INTEGER KREC,ISPEC,LSPEC,MEM,IFFR,ILFR,INCR,IFINC,NCHANX
      INTEGER INDX1,INDX2,KSP1,KSP2,I,J,K,NVAL,IPACK
      CHARACTER*80 HEAD1,HEAD2
      CHARACTER*13 HFNAM,OFNAM
C
C ICLO   : Close file indicator
C JOP    : Open file indicator
C IRC    : Return code
C IFRAME : Total nos. of frames
C IFRMAX : Nos. of frames in sequence
C IHFMAX : Nos. of header file in sequence
C KREC   : Output file record
C ISPEC  : First header file of sequence
C LSPEC  : Last header file in sequence
C MEM    : Positional or calibration data indicator
C IFFR   : First frame in sequence
C ILFR   : Last frame in sequence
C INCR   : Header file increment
C IFINC  : Frame increment
C HFNAM  : Header filename
C IMEM   : Output memory dataset
C NCHAN  : Nos. of data points in spectrum
C NFRAME : Nos. of time frames
C NCHANX :
C INDX1  :
C INDX2  :
C KSP1   :
C KSP2   :
C
      DATA  IMEM/1/ 
C
C-----------------------------------------------------------------------
      IF (XAXIS) THEN
         WRITE (IPRINT,*) 'Packing Sequence'
         CALL GETXAX (NCHANX,IRC)
         IF (IRC.NE.0) GOTO 999
      ENDIF
10    JOP=0
      ICLO=0
C
      CALL GETHDR (ITERM,IPRINT,IUNIT,HFNAM,ISPEC,LSPEC,INCR,MEM,IFFR,
     1             ILFR,IFINC,IHFMAX,IFRMAX,NCHAN,IRC)
      IF (IRC.NE.0) GOTO 999
C
C========FIND FINAL NOS. OF PACKED FRAMES
C
      IF (XAXIS) THEN
         IFRAME=0
         NVAL=NCHANX/2
         DO 20 I=1,NCHANX/2
            INDX1=(I-1)*2+1
            INDX2=INDX1+1
            KSP1=INT(XAX(INDX1))
            KSP2=INT(XAX(INDX2))
            IFFR=IABS(KSP1)
            ILFR=IABS(KSP2)
            IF (KSP1.LT.0) THEN
               IFRAME=IFRAME+(ILFR-IFFR+1)
            ELSE
               IFRAME=IFRAME+1
            ENDIF
 20      CONTINUE
      ELSE
         IPACK=IFINC
         IFRAME=(ILFR-IFFR+1)/IPACK
         NVAL=IFRAME
         KSP1=0
      ENDIF
C
C========PACK FRAMES
C
      IFINC=1
      CALL OPNFIL (JUNIT,HFNAM,ISPEC,MEM,IFFR,ILFR,NCHAN,NFRAME,IRC)
      KREC=0
      DO 50 I=1,NVAL
         IF (XAXIS) THEN
            INDX1=(I-1)*2+1
            INDX2=INDX1+1
            KSP1=INT(XAX(INDX1))
            KSP2=INT(XAX(INDX2))
            IPACK=IABS(KSP2)-IABS(KSP1)+1
         ENDIF
         CALL FILL (SP3,NCHAN,0.0)
         DO 40 J=1,IPACK
            READ (JUNIT,REC=IFFR) (SP1(K),K=1,NCHAN)
            IFFR=IFFR+IFINC
            IF (KSP1.GE.0) THEN
               DO 30 K=1,NCHAN
                  SP3(K)=SP3(K)+SP1(K)
                  IF (J.EQ.IPACK) SP1(K)=SP3(K)/IPACK
30             CONTINUE
               IF (J.NE.IPACK) GOTO 40
            ENDIF
            KREC=KREC+1
            IF (KREC.EQ.1) CALL OUTFIL (ITERM,IPRINT,OFNAM,HEAD1,
     1                                  HEAD2,IRC)
            IF (IRC.NE.0) GOTO 60
            IF (KREC.EQ.IFRAME) ICLO=1
            CALL DAWRT (KUNIT,OFNAM,IMEM,NCHAN,IFRAME,HEAD1,HEAD2,
     1                  SP1,KREC,JOP,ICLO,IRC)
            IF (IRC.NE.0) GOTO 60
40       CONTINUE
50    CONTINUE
 60   CLOSE (UNIT=JUNIT)
      GOTO 10
999   RETURN
      END   

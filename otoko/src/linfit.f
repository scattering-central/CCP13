C     LAST UPDATE 16/03/89
C+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
C
      SUBROUTINE LINFIT (X,Y,SIGMAY,NPTS,MODE,A,SIGMAA,B,SIGMAB,R)
      IMPLICIT NONE
C
C  PURPOSE: LEAST SQUARES FIT TO A STRAIGHT LINE.
C
      REAL X(1),Y(1),SIGMAY(1),A,SIGMAA,B,SIGMAB,R
      INTEGER NPTS,MODE

C X      : ABSCISSA DATA
C Y      : ORDINATE DATA
C SIGMAY : ARRAY OF STANDARD DEVIATIONS FOR Y VALUES
C NPTS   : NUMBER OF PAIRS OF DATA POINTS
C MODE   : DETERMINES THE METHOD OF WEIGHTING  VALUES
C               +1 (INSTRUMENTAL)  WT(I)=1./SIGMAY(I)**2
C                0 (NO WEIGHTING)  WT(I)=1.
C               -1 (STATISTICAL)  WT(I)=1./X(I)
C A      : Y INTERCEPT OF STRAIGHT LINE
C SIGMAA : STANDARD DEVIATION OF A
C B      : SLOPE OF FITTED LINE
C SIGMAB : STANDARD DEVIATION OF B
C R      : LINEAR CORRELATIION COEFFICIENT
C
C CALLS   0:
C
C-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-
C LOCAL VARIABLES:
C
      DOUBLE PRECISION WEIGHT
      DOUBLE PRECISION SUM,SUMX,SUMY,SUMX2,SUMXY,SUMY2,DELTA,VARNCE
      REAL    XI,YI,FREE
      INTEGER I
C
C WEIGHT : WEIGHTING FACTORS
C SUM    : ACCUMULATED SUM OF WEIGHTS
C SUMX   : ACCUMULATED SUM OF WEIGHTED X DATA
C SUMY   : ACCUMULATED SUM OF WEIGHTED Y DATA
C SUMX2  : ACCUMULATED SUM OF WEIGHTED Y*Y DATA
C SUMXY  : ACCUMULATED SUM OF WEIGHTED X*Y DATA
C SUMY2  : ACCUMULATED SUM OF WEIGHTED Y*Y DATA
C DELTA  : WEIGHTING FOR COEFFICIENTS
C VARNCE : VARIANCE
C
C-----------------------------------------------------------------------
C
C========ACCUMULATING SUMS
C
      SUM=0
      SUMX=0
      SUMY=0
      SUMX2=0
      SUMXY=0
      SUMY2=0
C
      DO 70 I=1,NPTS
         XI = X(I)
         YI = Y(I)
         IF(MODE) 10,40,50
10       IF (YI) 30,40,20
20          WEIGHT=1./(YI)
            GOTO 60
30          WEIGHT=1./(-YI)
            GOTO 60
40          WEIGHT=1.
            GOTO 60
50          WEIGHT=1./SIGMAY(I)**2
C
60       SUM = SUM + WEIGHT
         SUMX = SUMX + WEIGHT*XI
         SUMY = SUMY + WEIGHT*YI
         SUMX2 = SUMX2 + WEIGHT*XI*XI
         SUMXY = SUMXY + WEIGHT*XI*YI
         SUMY2 = SUMY2 + WEIGHT*YI*YI
70    CONTINUE
C        
C========CALCULATING COEFFICIENTS & STANDARD DEVIATIONS
C
      DELTA = SUM*SUMX2 - SUMX*SUMX
      A     = (SUMX2*SUMY - SUMX*SUMXY)/DELTA
      B     = (SUMXY*SUM - SUMX*SUMY)/DELTA
      IF (MODE) 80,90,80
80       VARNCE=1.0
         GOTO 100
90       FREE = REAL(NPTS-2)
         VARNCE = (SUMY2 + A*A*SUM + B*B*SUMX2
     1            -2.0*(A*SUMY + B*SUMXY - A*B*SUMX))/FREE
100   SIGMAA = DSQRT(VARNCE*SUMX2/DELTA)
      SIGMAB = DSQRT(VARNCE*SUM/DELTA)
      R = (SUM*SUMXY-SUMX*SUMY)/DSQRT(DELTA*(SUM*SUMY2-SUMY*SUMY))
      RETURN
C
      END

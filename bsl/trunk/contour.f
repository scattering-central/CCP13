C     LAST UPDATE 12/06/89
C+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
C
      SUBROUTINE CONTOUR (ITERM,IPRINT,DATA,NPIX,NRAST,IXC,IYC,TITLE)
      IMPLICIT   NONE
C
C PURPOSE: PLOT A CONTOUR MAP OF THAT IMAGE. THE MAP IS MADE UP OF A 
C          SET OF CONTOURS, EACH SET CONTAINING EQUALLY SPACED CONTOURS.
C          EACH SET HAS A DIFFERENT COLOUR, AND A LINE IS DISPLAYED DOWN
C          ONE SIDE OF THE IMAGE INDICATING THE RELATIONSHIP BETWEEN THE
C          RANGE COVERED BY A SET OF CONTOURS AND THE COLOUR IN WHICH
C          THAT SET IS DRAWN.
C
           REAL          DATA(1)
           INTEGER       ITERM,IPRINT,NPIX,NRAST,IXC,IYC
           CHARACTER*(*) TITLE
C
C ITERM  : TERMINAL I/O
C IPRINT : TERMINAL I/O
C DATA   : COMPLETE IMAGE DATA
C NPIX   : NOS. OF PIXELS
C NRAST  : NOS. OF RASTERS
C IXC    : FIRST PIXEL  (FOR PLOTTING AXIS ONLY)
C IYC    : FIRST RASTER (FOR PLOTTING AXIS ONLY)
C TITLE  : PLOT TITLE
C
C-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-
C LOCAL VARIABLES:
C
      REAL         VALUE(10),YPOS
      REAL         ZMX,ZMN,CLEVLS(128),STRTL,STOPL,FACTOR
      INTEGER      IRC,NVAL,LNUM,NCOL,LENGTH,I,K,L
      INTEGER      ISTRTX,ISTOPX,ISTRTY,ISTOPY
      CHARACTER*10 STRNUM
C
C VALUE  : TERMINAL INPUT
C ZMX    : MAXIMUM VALUE IN Z
C ZMN    : MINIMUM VALUE IN Z
C SURFAS : THE CONTOUR IMAGE (IN GHOST-COMPATIBLE FORM)
C CLEVLS : SET OF CONTOUR LEVELS
C STRTL  : FIRST CONTOUR LEVEL TO BE PLOTTED
C STOPL  : LAST     "      "   "  "     "
C FACTOR : A FACTOR
C IRC    : RETURN CODE
C NVAL   : NOS. OF VALUES ENTERED AT TERMINAL
C LNUM   : NO. OF LEVELS TO BE PLOTTED
C NCOL   : THE CURRENT COLOUR
C ISTRTX : START X POSITION FOR PLOTTING PART OF THE CONTOUR
C ISTOPX : END   "    "      "     "      "   "   "     "
C ISTRTY : START Y    "      "     "      "   "   "     "
C ISTOPY : END   "    "      "     "      "   "   "     "
C
C-----------------------------------------------------------------------
C
C========FIND AND DISPLAY MINMAX LEVELS OF IMAGE
C
      NCOL=0
      ZMX=DATA(1)
      ZMN=DATA(1)
      DO 10 L=1,NPIX*NRAST
         ZMX=MAX (ZMX,DATA(L))
         ZMN=MIN (ZMN,DATA(L))
10    CONTINUE
C
C========GET DATA FOR CONTOURS
C
30    WRITE (IPRINT,1000) ZMN,ZMX
      STRTL=ZMN
      STOPL=ZMX
      CALL GETVAL (ITERM,VALUE,NVAL,IRC)
      IF (IRC.EQ.1) GOTO 999
      IF (IRC.EQ.2) GOTO 30
      IF (NVAL.GT.0) STRTL=VALUE(1)
      IF (NVAL.GT.1) STOPL=VALUE(2)
      IF (STRTL.EQ.STOPL) THEN
         CALL ERRMSG ('Error: No difference in levels chosen')
         GOTO 30
      ENDIF
40    WRITE (IPRINT,1010)
      LNUM=10
      CALL GETVAL (ITERM,VALUE,NVAL,IRC)
      IF (IRC.NE.0) GOTO 40
      IF (NVAL.GT.0) LNUM=INT(VALUE(1))
      IF (LNUM.LT.2.OR.LNUM.GT.128) THEN
         CALL ERRMSG ('Error: Levels must be in range 2 - 128')
         GOTO 40
      ENDIF
      FACTOR=(STRTL-STOPL)/(LNUM-1)
      NCOL=NCOL+1
C
C========AVOID DRAWING IN WHITE, OR IN AN UNDEFINED COLOUR
C
      IF (NCOL.EQ.5) NCOL=6
      IF (NCOL.EQ.9) NCOL=1
C
C========SET UP GHOST COMPATIBLE DATA FOR CONTOURS
C
      DO 50 K=1,LNUM
         CLEVLS(K)=STOPL+(K-1)*FACTOR
50    CONTINUE
C
C========INITIALISE PLOTTING
C
      CALL GRMODE
      CALL PLOTON
      CALL PSPACE (0.25,1.1,0.175,0.9)
      CALL ERASE
      CALL CTRMAG (20)
      CALL WINFOL
C
C========PUT AXIS ANNOTATION & TITLE TO PLOT  
C 
      CALL MAP (0.0,1.0,0.0,1.0)
      CALL PCSCEN (0.5,1.05,TITLE(1:LENGTH(TITLE)))
      YPOS=1.0
      CALL PLOTCS (1.05,YPOS,'LEVELS')
      DO 60 I=1,LNUM
         YPOS=YPOS-0.05
         WRITE (STRNUM,'(G10.4)') CLEVLS(I)
         CALL PLOTCS (1.05,YPOS,STRNUM)
60    CONTINUE
      CALL PICNOW
      CALL MAP (REAL(IXC),REAL(IXC+NPIX-1),REAL(IYC),REAL(IYC+NRAST-1))
      CALL AXORIG (REAL(IXC),REAL(IYC))
      CALL AXESSI (0.,0.)
      CALL PICNOW
      CALL MAP (1.0,REAL(NPIX),1.0,REAL(NRAST))
      CALL POSITN (REAL(NPIX),1.0)
      CALL JOIN (REAL(NPIX),REAL(NRAST))
      CALL JOIN (1.0,REAL(NRAST))
      CALL PICNOW
C
C========PLOT CONTOURS WITH ANNOTATION DISABLED
C
      CALL CONOTA (0)
      ISTRTX=1
      ISTRTY=1
70    ISTOPX=MIN(ISTRTX+127,NPIX)
      ISTOPY=MIN(ISTRTY+127,NRAST)
      CALL WINDOW (REAL(ISTRTX),REAL(ISTOPX),
     &             REAL(ISTRTY),REAL(ISTOPY))
      CALL CONTRA (DATA,ISTRTX,ISTOPX,NPIX,ISTRTY,
     &             ISTOPY,NRAST,CLEVLS,1,LNUM)
      CALL PICNOW
      ISTRTX=ISTRTX+128
      IF (ISTRTX.GE.NPIX) THEN
         ISTRTX=1
         ISTRTY=ISTRTY+128
         IF (ISTRTY.GE.NRAST) GOTO 80
      ENDIF
      GOTO 70
80    CALL TRMODE
      CALL SAVPLO (ITERM,IPRINT)
      GOTO 30
999   RETURN
C
1000  FORMAT (' Enter first & last levels or <CTRL-Z>',
     &        ' [',G9.4,',',G9.4,']: ',$)
1010  FORMAT (' Enter number of levels [10]: ',$)
      END

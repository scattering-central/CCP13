C     LAST UPDATE 15/09/88
C+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
C
      SUBROUTINE SHFLIM (ITERM,IPRINT,NPIX,NRAST,IFPIX,ILPIX,
     &                   IFRAST,ILRAST,IXSH,IYSH,IRC)
      IMPLICIT NONE
C
C PURPOSE: ALLOW USER TO SELECT A SECTION OF THE IMAGE TO SHIFT
C
      INTEGER ITERM,IPRINT,NPIX,NRAST,IFPIX,ILPIX,IFRAST,ILRAST
      INTEGER IXSH,IYSH,IRC
C
C ITERM  : TERMINAL INPUT
C IPRINT : TERMINAL OUTPUT
C NPIX   : NOS. OF PIXELS IN IMAGE
C NRAST  : NOS. OF RASTERS IN IMAGE
C IFPIX  : FIRST PIXEL OF SECTION
C ILPIX  : LAST PIXEL OF SECTION
C IFRAST : FIRST RASTER OF SECTION
C ILRAST : LAST RASTER OF SECTION
C IXSH   : NOS. OF PIXELS TO SHIFT
C IYSH   : NOS. OF RASTERS TO SHIFT
C IRC    : RETURN CODE
C
C CALLS  2: ERRMSG , GETVAL
C
C-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-
C LOCAL VARIABLES:
C
      REAL    VALUE(10)
      INTEGER ITEMP,NVAL
C
C-----------------------------------------------------------------------
C
C========GET SHIFT PARAMETERS
C
20    IFPIX=1
      ILPIX=NPIX
      WRITE (IPRINT,1000) NPIX
      CALL GETVAL (ITERM,VALUE,NVAL,IRC)
      IF (IRC.EQ.1) GOTO 999
      IF (IRC.EQ.2) GOTO 20
      IF (NVAL.GE.1) IFPIX=INT(VALUE(1))
      IF (NVAL.GE.2) ILPIX=INT(VALUE(2))
C
30    IXSH=0.0
      WRITE (IPRINT,1010) 
      CALL GETVAL (ITERM,VALUE,NVAL,IRC)
      IF (IRC.EQ.1) GOTO 999
      IF (IRC.EQ.2) GOTO 30
      IF (NVAL.GE.0) IXSH=INT(VALUE(1))
C
C======CHECK VALUES LIE WITHIN CORRECT LIMITS
C
      IF (IFPIX.LT.1) IFPIX=1
      IF (ILPIX.LT.1) ILPIX=1
      IF (IFPIX.GT.NPIX) IFPIX=NPIX
      IF (ILPIX.GT.NPIX) ILPIX=NPIX
      IF (ABS(IXSH).GT.NPIX) IXSH=NPIX
      IF (ILPIX.LT.IFPIX) THEN
         ITEMP=IFPIX
         IFPIX=ILPIX
         ILPIX=ITEMP
      ENDIF
C
25    IFRAST=1
      ILRAST=NRAST
      WRITE (IPRINT,1020) NRAST
      CALL GETVAL (ITERM,VALUE,NVAL,IRC)
      IF (IRC.EQ.1) GOTO 999
      IF (IRC.EQ.2) GOTO 25
      IF (NVAL.GE.1) IFRAST=INT(VALUE(1))
      IF (NVAL.GE.2) ILRAST=INT(VALUE(2))
C
35    IYSH=0
      WRITE (IPRINT,1030) 
      CALL GETVAL (ITERM,VALUE,NVAL,IRC)
      IF (IRC.EQ.1) GOTO 999
      IF (IRC.EQ.2) GOTO 35
      IF (NVAL.GE.1) IYSH=INT(VALUE(1))
      IF (IFRAST.LT.1) IFRAST=1
      IF (ILRAST.LT.1) ILRAST=1
      IF (IFRAST.GT.NRAST) IFRAST=NRAST
      IF (ILRAST.GT.NRAST) ILRAST=NRAST
      IF (ABS(IYSH).GT.NRAST) IYSH=NRAST
      IF (ILRAST.LT.IFRAST) THEN
         ITEMP=IFRAST
         IFRAST=ILRAST
         ILRAST=ITEMP
      ENDIF
999   RETURN
C
1000  FORMAT (' Enter first & last pixels to shift [1,',I4,']: ',$)
1010  FORMAT (' Enter shift +ve for right -ve for left [0]: ',$)
1020  FORMAT (' Enter first & last rasters to shift [1,',I4,']: ',$)
1030  FORMAT (' Enter shift +ve for down -ve for up [0]: ',$)
      END

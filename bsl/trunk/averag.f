C     LAST UPDATE 09/06/89
C+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
C
      SUBROUTINE AVERAG
      IMPLICIT   NONE
C
C PURPOSE: AVERAGE A SERIES OF IMAGES.
C
      INCLUDE 'COMMON.FOR'
C
C Calls   8: IMDISP , WFRAME , RFRAME , OUTFIL
C            GETHDR , OPNFIL , OPNNEW , FILL
C Called by: BSL
C
C-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-
C LOCAL VARIABLES:
C
      INTEGER      ISPEC,LSPEC,INCR,IFFR,ILFR,IFINC,IHFMAX,IFRMAX,IFRAME
      INTEGER      NPIX,NRAST,NFRAME,KPIC,MEM,IMEM
      INTEGER      IRC,I,J,K
      CHARACTER*80 HEAD1,HEAD2
      CHARACTER*13 HFNAM,OFNAM
C
C ISPEC  : FIRST HEADER FILE IN SEQUENCE
C LSPEC  : LAST HEADER FILE IN SEQUENCE
C INCR   : HEADER FILE INCREMENT
C IFFR   : FIRST IMAGE IN SEQUENCE
C ILFR   : LAST IMAGE IN SEQUENCE
C IFINC  : IMAGE INCREMENT
C IHFMAX : NOS. OF HEADER FILES
C IFRMAX : NOS. OF IMAGES
C IFRAME : TOTAL NOS. OF IMAGES TO PROCESS
C NPIX   : NOS. OF PIXELS IN IMAGE
C NRAST  : NOS. OF RASTERS IN IMAGE
C NFRAME : NOS. OF FRAMES IN DATASET
C KPIC   : CURRENT IMAGE NOS.
C MEM    : POSITIONAL OR CALIBRATION DATA
C IMEM   : POSITIONAL OR CALIBRATION DATA OUTPUT
C IRC    : RETURN CODE
C HEAD1  : HEADER RECORD 1
C HEAD2  : HEADER RECORD 2
C HFNAM  : HEADER FILENAME
C OFNAM  : OUTPUT HEADER FILENAME
C
      DATA IMEM/1/
C
C-----------------------------------------------------------------------
10    KPIC=0
C
C========GET HEADER & BINARY FILE ATTRIBUTES
C
20    CALL GETHDR (ITERM,IPRINT,IUNIT,HFNAM,ISPEC,LSPEC,INCR,MEM,IFFR,
     &             ILFR,IFINC,IHFMAX,IFRMAX,NPIX,NRAST,IRC)
      IF (IRC.EQ.0) THEN
C
C========OPEN FILE AND READ REQUESTED FRAME
C
         IF (KPIC.EQ.0) CALL FILL (SP2,NPIX*NRAST,0.0)
         IFRAME=IHFMAX+IFRMAX-1
         DO 40 I=1,IHFMAX 
C
            CALL OPNFIL (JUNIT,HFNAM,ISPEC,MEM,IFFR,ILFR,NPIX,NRAST,
     &                   NFRAME,IRC)
            IF (IRC.EQ.0) THEN
               ISPEC=ISPEC+INCR
               DO 30 J=1,IFRMAX
C
                  CALL RFRAME (JUNIT,IFFR,NPIX,NRAST,SP1,IRC)
                  IF (IRC.NE.0) THEN
                     CALL FCLOSE (KUNIT)
                     CALL FCLOSE (JUNIT)
                     GOTO 10
                  ENDIF
                  IFFR=IFFR+IFINC
                  KPIC=KPIC+1
C
                  DO 25 K=1,NPIX*NRAST
                     SP2(K)=SP2(K)+SP1(K)
25                CONTINUE
30             CONTINUE
            ENDIF
            CALL FCLOSE (JUNIT)
40       CONTINUE
C
      ELSE
         IF (KPIC.NE.0) THEN
            DO 45 I=1,NPIX*NRAST
               SP2(I)=SP2(I)/REAL(KPIC)
45          CONTINUE
            IFRAME=1
            CALL IMDISP (ITERM,IPRINT,SP2,NPIX,NRAST)
            CALL OUTFIL (ITERM,IPRINT,OFNAM,HEAD1,HEAD2,IRC)
            IF (IRC.NE.0) GOTO 50
            CALL OPNNEW (KUNIT,NPIX,NRAST,IFRAME,OFNAM,IMEM,HEAD1,
     &                   HEAD2,IRC)
            IF (IRC.NE.0) GOTO 50
            CALL WFRAME (KUNIT,IFRAME,NPIX,NRAST,SP2,IRC)
            IF (IRC.NE.0) GOTO 50
50          CALL FCLOSE (KUNIT)
            CALL FCLOSE (JUNIT)
            GOTO 10
         ELSE
            RETURN
         ENDIF
      ENDIF
      GOTO 20
      END
